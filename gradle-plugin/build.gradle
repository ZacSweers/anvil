plugins {
  id 'java-gradle-plugin'
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.kotlin.dokka)
  alias(libs.plugins.gradlePublish)
  alias(libs.plugins.mavenPublish)
  alias(libs.plugins.ktlint)
}

repositories {
  google()
  mavenCentral()
}

apply from: rootProject.file('copy_properties.gradle')
apply from: rootProject.file('generate_build_properties.gradle')

gradlePlugin {
  plugins {
    anvilPlugin {
      id = "com.squareup.anvil"
      displayName = project.findProperty('POM_NAME')
      implementationClass = "com.squareup.anvil.plugin.AnvilPlugin"
      description = project.findProperty('POM_DESCRIPTION')
    }
  }
}

pluginBundle {
  website = project.findProperty('POM_URL')
  vcsUrl = project.findProperty('POM_SCM_URL')
  tags = ['dagger2', 'dagger2-android', 'kotlin', 'kotlin-compiler-plugin']
}

// Use JDK 11 but target Java 8 for maximum compatibility.
java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

tasks.withType(JavaCompile).configureEach {
  options.release.set(8)
}

//noinspection UnnecessaryQualifiedReference
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask).configureEach {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8

    // Because Gradle hardcodes Kotlin versions we have warnings during compilation.
    allWarningsAsErrors = false
  }
}

kotlin {
  explicitApi()
}

ktlint {
  version = libs.versions.ktlint.get()
}

dependencies {
  implementation libs.kotlin.gradlePlugin
  implementation libs.kotlin.gradlePluginApi

  // Necessary to bump a transitive dependency.
  compileOnly libs.kotlin.reflect

  // Compile only so that Java / Kotlin modules don't download it unnecessarily.
  compileOnly libs.agp
}
