import com.android.build.gradle.api.UnitTestVariant
import com.squareup.anvil.plugin.AndroidVariantFilter

plugins {
  id 'org.jetbrains.kotlin.multiplatform'
  id 'com.android.library'
  id 'org.jetbrains.kotlin.kapt'
  id 'com.squareup.anvil'
}

android {
  compileSdk = 30
  namespace = "com.squareup.anvil.mpp"

  defaultConfig {
    minSdk = 24
    targetSdk = 30
    versionCode = 1
    versionName = "1.0.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lint {
    it.warningsAsErrors = true
  }

  variantFilter { variant ->
    //noinspection UnnecessaryQualifiedReference
    if (variant.buildType.name == com.android.builder.core.BuilderConstants.RELEASE) {
      variant.setIgnore(true)
    }
  }
}

kotlin {
  android()

  sourceSets {
    androidMain {
      dependencies {
        implementation deps.dagger2.dagger
      }
    }

    androidTest {
      dependencies {
        implementation deps.junit
        implementation deps.truth
      }
    }
  }
}

anvil {
  variantFilter { filter ->
    if (filter instanceof AndroidVariantFilter &&
        filter.androidVariant instanceof UnitTestVariant) {
      ignore = true
    }
  }
}

dependencies {
  kapt deps.dagger2.compiler

  // This dependency isn't needed. It's only here for testing purposes (this is still an
  // integration test).
  anvilAndroidTest project(':integration-tests:code-generator')
}
